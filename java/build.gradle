plugins {
	id 'java'
	id 'java-library'
	id 'maven-publish'
}

allprojects {
	
	apply plugin: 'java'
	apply plugin: 'java-library'
	
	group 'cc.sukazyo.untitled'
	version PROJ_VERSION
	
	repositories {
		mavenCentral()
		maven { name 'github'; url 'https://jitpack.io' }
		maven { name '-ws'; url 'https://mvn.sukazyo.cc' }
	}
	
	dependencies {
		
		compileOnlyApi "com.github.spotbugs:spotbugs-annotations:${libSpotbugsVersion}"
		
		testImplementation platform("org.junit:junit-bom:${libJunitVersion}")
		testImplementation 'org.junit.jupiter:junit-jupiter'
		
	}
	
	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
	}
	
	tasks.withType(Javadoc) {
		options.encoding = 'UTF-8'
		options.docEncoding = 'UTF-8'
		options.charSet = 'UTF-8'
	}
	
	test {
		useJUnitPlatform()
	}
	
	java {
		
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
		
		withSourcesJar()
	
	}
	
}

dependencies {
	
	implementation group: 'com.github.Eyre-S', name: 'ResourceTools', version: '0.2.2'
	implementation group: 'net.coobird', name: 'thumbnailator', version: '0.4.13'
	implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.1'
	
}

subprojects {
	
	apply plugin: 'maven-publish'
	
	project.ext.artifactId = project.name
	
	publishing {
		repositories {
			maven {
				name 'builds'
				url = "file://" + new File("./build/publishing").getAbsolutePath()
			}
			maven {
				name '-ws-'
				url publishMvnRepoUrl
				credentials {
					username publishMvnRepoUsername
					password publishMvnRepoPassword
				}
			}
		}
		publications {
			main (MavenPublication) {
				from components.java
				groupId = project.group
				artifactId = project.ext.artifactId
				version = project.version
			}
		}
	}
	
}
